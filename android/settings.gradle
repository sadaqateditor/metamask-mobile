pluginManagement {
  includeBuild('../node_modules/@react-native/gradle-plugin')
  repositories {
    gradlePluginPortal()
    mavenLocal()
    google()
    mavenCentral()
  }
}

plugins {
  id("com.facebook.react.settings")
}

extensions.configure(com.facebook.react.ReactSettingsExtension) { ex ->
  if (System.getenv('EXPO_USE_COMMUNITY_AUTOLINKING') == '1') {
    ex.autolinkLibrariesFromCommand()
  } else {
    def command = [
      'node',
      '--no-warnings',
      '--eval',
      'require(require.resolve("expo-modules-autolinking", { paths: [require.resolve("expo/package.json")] }))(process.argv.slice(1))',
      'react-native-config',
      '--json',
      '--platform',
      'android'
    ].toList()
    ex.autolinkLibrariesFromCommand(command)
  }
}

rootProject.name = 'MetaMask'

dependencyResolutionManagement {
  versionCatalogs {
    reactAndroidLibs {
      from(files("../node_modules/react-native/gradle/libs.versions.toml"))
    }
  }
}

include ':app'
includeBuild('../node_modules/@react-native/gradle-plugin')
includeBuild('../node_modules/@react-native') {}

include ':lottie-react-native'
project(':lottie-react-native').projectDir = new File(rootProject.projectDir, '../node_modules/lottie-react-native/src/android')

include ':react-native-gesture-handler'
project(':react-native-gesture-handler').projectDir = new File(rootProject.projectDir, '../node_modules/react-native-gesture-handler/android')

apply from: '../node_modules/expo/scripts/autolinking.gradle'
useExpoModules()
